buildscript {
    repositories {
    	maven { url  = 'https://plugins.gradle.org/m2/' }
        maven { url = 'https://maven.parchmentmc.org' }
    }
    dependencies {
        classpath "gradle.plugin.com.github.johnrengelman:shadow:7.1.0"
    }
}

plugins {
    id 'fabric-loom' version '1.0-SNAPSHOT'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id "com.modrinth.minotaur" version "2.+"
}

import groovy.json.JsonSlurper
import groovy.json.JsonOutput
import com.modrinth.minotaur.dependencies.ModDependency

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archivesBaseName
version = project.modVersion
group = project.mavenGroup

repositories {
	maven { url "https://cfa2.cursemaven.com" }
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.terraformersmc.com" }
    maven { url 'https://dl.cloudsmith.io/public/azuredoom-mods/azurelib/maven/' }
    maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
    maven { url 'https://maven.blamejared.com' }
    maven { url 'https://plugins.gradle.org/m2/' }
    maven { url 'https://maven.gegy.dev' }
    maven { url 'https://maven.enginehub.org/repo/' }
    maven { url  = 'https://plugins.gradle.org/m2/' }
	maven { url 'https://api.modrinth.com/maven' }
}

configurations {
    shade
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraftVersion}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loaderVersion}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabricVersion}"
  	modImplementation 'mod.azure.azurelib:azurelib-fabric-1.19.2:1.0.2'
	modImplementation "maven.modrinth:midnightlib:1.0.0-fabric"
    shade group: 'maven.modrinth', name: 'midnightlib', version: '1.0.0-fabric'
	modImplementation ("com.terraformersmc:modmenu:4.1.2")
	modImplementation "curse.maven:SBL-661293:4312490-sources-4312491"
	modImplementation 'software.bernie.geckolib:geckolib-fabric-1.19:3.1.39'
}

shadowJar {
    configurations = [project.configurations.shade]
    from sourceSets.main.output
    duplicatesStrategy = 'exclude'
    relocate 'eu.midnightdust', 'mod.azure.gigeresque.shadowed.midnightlib'
    classifier 'dev'
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

processResources {
	doLast {
        def jsonMinifyStart = System.currentTimeMillis()
        def jsonMinified = 0
        def jsonBytesSaved = 0
        fileTree(dir: outputs.files.asPath, include: '**/*.json').each { 
            File file = it
            jsonMinified++
            def oldLength = file.length()
            file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
            jsonBytesSaved += oldLength - file.length()
        }
        println('Minified ' + jsonMinified + ' json files. Saved ' + jsonBytesSaved + ' bytes. Took ' + (System.currentTimeMillis() - jsonMinifyStart) + 'ms.')
    }
}

loom {
	accessWidenerPath = file("src/main/resources/gigeresque.accesswidener")
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

artifacts {
    archives shadowJar
}

remapJar {
    dependsOn(shadowJar)
    inputFile = shadowJar.archiveFile
}

if (file('key.properties').exists()) {
    curseforge {

    	def curseProp = new Properties()
    	File secretPropsFile = file("key.properties")
    	curseProp.load(secretPropsFile.newInputStream())

    	project {
        	apiKey = curseProp.getProperty('curseKey')
        	id = "535416"
        	releaseType = "beta"
        	changelogType = 'text'
        	changelog = file('CHANGELOG.txt')
			addGameVersion("1.19.2")
			addGameVersion("Fabric")
			addGameVersion("Quilt")
			addGameVersion("Java 17")

        	relations {
            	requiredDependency 'fabric-api'  
            	requiredDependency 'azurelib'  
            	requiredDependency 'smartbrainlib' 
            	optionalDependency 'modmenu'
            	optionalDependency 'dripsounds-fabric'
            	optionalDependency 'sound-physics-remastered'
        	}
        	mainArtifact(remapJar)

        	afterEvaluate {
            	uploadTask.dependsOn("remapJar")
        	}
    	}
    	options {
    		//debug = true
        	forgeGradleIntegration = false
    	}
    }
}

if (file('key.properties').exists()) {
	modrinth {
		def modrinthProp = new Properties()
		File secretPropsFile = file("key.properties")
		modrinthProp.load(secretPropsFile.newInputStream())
		
		token = modrinthProp.getProperty('modrinthKey')
		projectId = '2kXK3iBI' 
		versionNumber = project.mod_version 
		versionType = "beta"
		versionName = 'Fabric/Quilt 1.19.2'
		uploadFile = remapJar 
		changelog = rootProject.file("changelog.txt").text
		gameVersions = ['1.19.2']
		loaders = ['fabric','quilt']
		dependencies { 
        	required.project "azurelib"
        	required.project "fabric-api"
        	required.project "smartbrainlib"
            optional.project "modmenu"
            optional.project "dripsounds-fabric"
            optional.project "sound-physics-remastered"
   		}
	}
}