plugins {
    id 'fabric-loom' version '1.6-SNAPSHOT'
    id 'maven-publish'
    id "me.modmuss50.mod-publish-plugin" version "0.4.0"
}

import groovy.json.JsonOutput
import groovy.json.JsonSlurper

base {
    archivesName = project.modName
}
version = project.modVersion
group = project.mavenGroup

repositories {
    maven { url "https://cfa2.cursemaven.com" }
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.terraformersmc.com" }
    maven { url "https://libs.azuredoom.com:4443/mods" }
    maven { url "https://dl.cloudsmith.io/public/tslat/sbl/maven/" }
    maven { url "https://maven.blamejared.com" }
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url "https://maven.gegy.dev" }
    maven { url "https://maven.enginehub.org/repo/" }
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url "https://api.modrinth.com/maven" }
    maven { url "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/" }
    maven { url "https://maven.parchmentmc.org" }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraftVersion}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-1.20.6:2024.06.02@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${project.loaderVersion}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabricVersion}"
    modImplementation "mod.azure.azurelib:azurelib-fabric-${project.minecraftVersion}:${project.azurelibVersion}"
    modImplementation "com.terraformersmc:modmenu:${project.modmenuVersion}"
    modImplementation "maven.modrinth:smartbrainlib:mY9xu8va"
    modImplementation "software.bernie.geckolib:geckolib-fabric-${project.minecraftVersion}:${project.geckolibVersion}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

processResources {
    doLast {
        def jsonMinifyStart = System.currentTimeMillis()
        def jsonMinified = 0
        def jsonBytesSaved = 0
        fileTree(dir: outputs.files.asPath, include: '**/*.json').each {
            File file = it
            jsonMinified++
            def oldLength = file.length()
            file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
            jsonBytesSaved += oldLength - file.length()
        }
        println('Minified ' + jsonMinified + ' json files. Saved ' + jsonBytesSaved + ' bytes. Took ' + (System.currentTimeMillis() - jsonMinifyStart) + 'ms.')
    }
}

loom {
    accessWidenerPath = file("src/main/resources/gigeresque.accesswidener")
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

if (file('key.properties').exists()) {
    publishMods {
        def releaseProp = new Properties()
        File secretPropsFile = file("key.properties")
        releaseProp.load(secretPropsFile.newInputStream())
        file = remapJar.archiveFile
        displayName = project.modVersion
        changelog = rootProject.file("changelog.md").text
        type = STABLE

        def cfoptions = curseforgeOptions {
            accessToken = releaseProp.getProperty("curseKey")
            minecraftVersions.add(project.minecraftVersion)
            projectId = project.curseforgeID
            projectSlug = "gigeresque" // Required for discord webhook
        }
        def mroptions = modrinthOptions {
            projectId = project.modrinthID
            accessToken = releaseProp.getProperty("modrinthKey")
            minecraftVersions.add(project.minecraftVersion)
        }
        curseforge("curseforgeFabric") {
            from cfoptions
            modLoaders.add("fabric")
            requires {
                slug = "fabric-api"
            }
            requires {
                slug = "azurelib"
            }
            requires {
                slug = "smartbrainlib"
            }
            optional {
                slug = "modmenu"
            }
        }
        modrinth("modrinthFabric") {
            from mroptions
            modLoaders.add("fabric")
            requires {
                slug = "fabric-api"
            }
            requires {
                slug = "azurelib"
            }
            requires {
                slug = "smartbrainlib"
            }
            optional {
                slug = "modmenu"
            }
        }
    }
}